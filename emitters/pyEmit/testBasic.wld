PingPong

/**
  * Simple example: user can start a message stream by calling the public
  * function initiateSend on Ping.  This starts a sequence of three
  * messages that increment counters on both endpoints, printing along the
  * way.
*/


Endpoint Ping;
Endpoint Pong;


Traces
{
        // specifies order of handlers that will be called
        Ping.one -> Pong.two -> Ping.three -> Pong.four;
}


/**** Data shared between both ends */
Shared
{
        //both of these are rarely used in this program.
        Ping Controls Number pingCounter = 0;
        Pong Controls Number pongCounter = 0;
}


/****  New endpoint *********/
Ping
{
        Number responses = 0;
        Bool something;
        
        Public Function initiateSend() Returns Nothing
        {
            Print('Initiating send');
            one();
        }
        
        MsgSend one () OutgoingMessage { Number responses }
        {
            pingCounter = pingCounter + 1;
            OutgoingMessage msg = {
              'responses' : responses
            };
            
            Send msg To Pong;
        }

        
        MsgReceive three
             IncomingMessage msg:
             {
                Number responses
             }
             OutgoingMessage:
             {
                 Number responses
             }
        {
        
            pingCounter = pingCounter + 1;  
            responses = responses + 1;
            OutgoingMessage msgResponse = {
              'responses' : responses
            };


            Print(msg['responses']);
            
            Send msgResponse To Pong;
        }
}


/****  New endpoint *********/
Pong
{
        Number responses = 0;
        
        MsgReceive two
             IncomingMessage msg:
             {
                Number responses
             }
             OutgoingMessage:
             {
                 Number responses
             }
        {
            pongCounter = pongCounter + 1;
            responses = responses + 1;
            OutgoingMessage msgResponse = {
              'responses' : responses
            };


            Print(msg['responses']);
            
            Send msgResponse To Ping;
        }

        MsgReceive four
             IncomingMessage msg:
             {
                Number responses
             }
             OutgoingMessage:
             {
                 Number responses
             }
        {
            pongCounter = pongCounter + 1;  
            responses = responses + 1;
            OutgoingMessage msgResponse = {
              'responses' : responses
            };

            Print(msg['responses']);
            
            Send msgResponse To Ping;
        }
}
