ChatRoom

Endpoint Chatter;
Endpoint Room;

Sequences{
    WriteMessage: Chatter.issue_write -> Room.write_response;
    UpdateChatter: Room.issue_update -> Chatter.update_response;
}



Peered
{
    Chatter controls Text chatter_name;
    // could also include profile, etc. info if
    // wanted to.
}


Sequence WriteMessage(Text msg)
{
    Chatter.issue_write
    {}

    Room.write_response
    {
        Text new_msg = '\n' + chatter_name + ': ';
        new_msg += msg + '\n';
        
        extCopy (msg_log + new_msg) to msg_log;
    }

    Room.onComplete
    {
        new_msg_callback();
    }
}

Sequence UpdateChatter(Text chat_log)
{
    Room.issue_update
    {}
    Chatter.update_response
    {}

    Chatter.onComplete
    {
        new_msg_to_display_callback(chat_log);
    }
}

Chatter
{
    Function (in: Text; returns: Nothing) new_msg_to_display_callback;
    
    onCreate(
        Text chatter_name_,
        Function (in: Text; returns: Nothing) new_msg_to_display_callback_)
    {
        chatter_name = chatter_name_;
        new_msg_to_display_callback = new_msg_to_display_callback_;
    }

    Public Function write_msg(Text msg)
    {
        issue_write(msg);
    }
}

Room
{
    External Text msg_log;
    Function (returns: Nothing) new_msg_callback;

    onCreate(
        External Text msg_log_,
        Function (returns: Nothing) new_msg_callback_)
    {
        extAssign msg_log_ to msg_log;
        new_msg_callback = new_msg_callback_;
    }

    Public Function update_msg_log()
    {
        issue_update(msg_log);
    }
}
